plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

android {
    namespace 'com.geelee.startup'
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    api project(":startup-annotation")
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"

    testImplementation project(":startup-processor")
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.robolectric:robolectric:4.9"
    testImplementation "androidx.test:runner:1.5.2"

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    println "Found secret props file, loading props"
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        ext[name] = value
    }
} else {
    println "No props file, loading env vars"
}

publishing {
    repositories {
        maven {
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
            url 'https://s01.oss.sonatype.org/content/repositories/releases/'
        }
    }

    publications {
        maven(MavenPublication) {
            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
            groupId 'io.github.geejoe'
            artifactId 'startup'
            version '0.0.4'
        }
    }
}
